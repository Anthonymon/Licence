Projet JTiger : TD 3
=====================

Mise en place
-------------

Récupérez et décompressez l'archive contenant le code fourni pour ce livrable
<http://tahiti.prism.uvsq.fr/tigergrader/static/support/livrable-T3.zip>.  Pour
compiler et soumettre le projet, faites comme dans les TPs précédents.

Pour faire ce TD partez de votre fichier ``src/parser/tiger.cup`` du T2B.

L'archive qui vous est fournie comporte un répertoire ``testcases``.  Ce
répertoire contient 51 fichiers Tiger complets suffixés par l'extension
```.tig``.  Ces tests sont fournis par A.W. Appel de l``université de Princeton
et créateur du langage Tiger <http://www.cs.princeton.edu/~appel/modern/>.

Pour chaque test, j'ai inclus un fichier éponyme suffixé par ``.tig.expected``.
Ce fichier contient la sortie attendue après l'exécution de l'analyseur
sémantique.

Les fichiers sont inclus pour vous aider à débugger votre parseur au fur à
mesure du TP.
 



### Reconnaître le langage de programmation Tiger 

Nous allons augmenter le parseur que vous avez codé lors du T2B pour
reconnaître l'ensemble du langage Tiger. Le langage Tiger est spécifié
complètement ci-dessous, dans la dernière section.  Prennez le temps de lire
les exemples et de bien comprendre comment le langage fonctionne.

Dans le TP précédent vous avez pris en compte les entiers, les séquences, ainsi que
toutes les expressions formées avec des opérateurs. Vous avez généré l'AST correspondant
à différents programmes d'entrées.

Dans ce TP nous étendons la grammaire pour prendre en compte l'ensemble des expressions,
les tests et structures de contrôles, les déclarations de variables et de types.

### Les déclarations de variables et fonctions

Pour l'instant dans notre grammaire on n'a pris en compte que des expressions,
c'est à dire des éléments du langage qui peuvent être évalués et retournent une
valeur. Maintenant on s'intéresse aux _déclarations_, qui définissent des
fonctions, variables ou types.

Avant de pouvoir manipuler des déclarations il faut définir une dernière expression,
appellée ``letExp``. Les blocs lets permettent de définir la portée d'un ensemble de
déclarations, par exemple, dans le bloc qui suit
        
        let
            var a : int := 0
        in
            a + 5
        end

on déclare une variable ``a`` visible uniquement à l'intérieur du bloc ``let``.


(@) Rajoutez une règle pour gérer les ``letExp``. Une expression _let_ à la grammaire suivante:

~~~  
exp ::= ... | letExp                  (letExp est de type Exp dans l'AST)

letExp ::= `let` decs `in` exps `end` (de type LetExp dans l'AST)

decs ::= { dec }                      (une suite de déclarations de type LinkedList<Dec>, voir question suivante)
~~~


(@) Rajoutez maintenant des règles pour gérer les déclarations. 
    L'ensemble des règles est donné ci-dessous, on a omis les déclarations de types que l'on fera ultérieurement,

~~~
dec ::= varDec                  (Les déclarations retournent un noeud AST de type Dec) 
      | funcsDec 


varDec ::=                      (AST de type VarDec) 
       `var` id typeannotation `:=` exp  

funcsDec ::= { funcDec }        (AST de type FuncsDec, une liste de funcDec)
funcDec ::=                     (AST de type FuncDec)
        `function` id `(` params `)` typeannotation `=` exp

params ::=                      (AST de type LinkedList<VarDec>, voir explication ci-dessous) 
       [ param { `,`  param } ] 

param ::= id `:` id             (AST de type VarDec) 
     

typeannotation ::= | `:` id     (Retourne un objet de type Id) 

id := ID                        (Retourne un objet de type Id)
~~~

Les objets de type Id sont définis dans ``src/jtiger/bind/Id``, ils permettent
de manipuler efficacement des identifiants (de variable, de types, de
fonctions). 

Pour nous faciliter la vie dans les étapes suivantes du compilateur, on a
décidé de modéliser les paramètres de fonction (``param``) dans l'AST comme un
objet de type ``VarDec``, avec l'identifiant dans l'attribut ``name`` et le
type dans l'attribut ``type``. L'attribut ``exp`` est initialisé à ``null``
pour les paramètres de fonctions.

Normalement après cette question votre parseur est capable de produire l'AST du programme ``testcases/test-tp1.tig``.

### Nouvelles Expressions

Maintenant il s'agit de completer les expressions que votre parseur reconnaît.
Normalement vous avez déjà implémenté ``letExp``, ``negExp``, ``opExp``, ``seqExp``, ``intExp``.
Voici la liste de toutes les expressions du langage Tiger qu'il reste à implémenter

~~~
exp::= nilExp
   | stringExp
   | var
   | assignExp
   | callExp
~~~


(@) Implémentez nilExp 

~~~
    nilExp::= `nil`      (AST de type NilExp)
~~~

(@) Implémentez stringExp

~~~
    stringExp::= string   (AST de type StringExp)
~~~

(@) Implémentez les variables.
    Il y a trois sortes de variables: les variables simples (``a``),
    les variables de tableau (``a[3]``) et les variables de structure (``a.champ``).

~~~
    var ::= simpleVar            (AST de type Var)
          | arrayVar 
          | recordVar

    simpleVar ::= id             (AST de type SimpleVar)

    recordVar ::= var `.` id     (AST de type RecordVar)
    
    arrayVar ::= var `[` exp `]` (AST de type ArrayVar)
~~~

(@) Implémentez l'affectation de variable

~~~
    assignExp::= var `:=` exp        (AST de type AssignExp)
~~~

(@) Implémentez les appels de fonction

~~~
    callExp::= id `(` arguments `)`            (AST de type CallExp)

    arguments::= [ argument { `,` argument } ] (AST de type LinkedList<Exp>, 
                                                liste possiblement vide d'arguments)
    argument::= exp                            (AST de type Exp, un argument est 
                                                toute expression valide)
~~~


Normalement, arrivés ici, votre parseur peut reconnaître les programmes test 6,7,13,18,19,24,25,26,27,31,34,35,36,37,39,40,43.

### Les structures de contrôle

Les structures de contrôle sont traitées comme des expressions dans l'AST.
Cette partie consiste à les implémenter.

~~~
exp::=
   | breakExp  (AST de type Exp)
   | ifExp
   | whileExp
   | forExp
~~~

(@) Implémentez breakExp

~~~
    breakExp::= break    (AST de type BreakExp)
~~~ 

(@) Implémentez ifExp, whileExp et forExp:

~~~
    forExp::= `for` id `:=` exp `to` exp `do` exp (AST de type ForExp)
    whileExp::= `while` exp `do` exp              (AST de type WhileExp) 
    ifExp::= `if` exp `then` exp                  (AST de type IfExp, avec le champ else initialisé à null)
         |   `if` exp `then` exp `else` exp       (AST de type IfExp)
~~~


Normalement, arrivés ici, votre parseur peut reconnaître les programmes test 4,8,9,10,11,12,15,20,21.


### Les types

Dans cette partie vous aller implémenter les déclaration de types.

(@) Tout d'abord rajoutez les règles suivantes pour pouvoir déclarer des types 

~~~
    dec ::= typesDec 

    typesDec ::= { typeDec }        (AST de type TypesDec, 
                                     un ensemble de déclarations de type)
    
    typeDec ::= `type` id `=` type  (AST de type TypeDec)

    type ::= nameType               (AST de type TypeDef)
           | arrayType 
           | recordType
~~~
    
Tiger autorise à definir des types alias (ou ``nameType``). Par exemple,

~~~ 
    let 
        type myint = int
        var a : myint := 0
    in
        a
    end
~~~
    
(@) Implémentez les ``nametype``

~~~
    nameType ::= id    (AST de type NameType)
~~~

Tiger autorise à définir des types tableaux (``arrayType``). Par exemple,

~~~
    let
        type tableau = array of int
        var a : tableau := tableau [10] of 0
    in
        a[1] := 42
    end
~~~

Ce programme déclare un type pour les tableaux d'entiers, puis déclare une
variable tableau `a` avec 10 éléments initialisés à 0.

(@) Implémentez les types tableaux.

~~~
    arrayType::= `array` `of` id              (AST de type ArrayType)

    Il faut aussi créer une nouvelle expression pour initialiser les tableaux,
    comme dans l'exemple ci-dessus.

    exp::= arrayExp

    arrayExp::= id `[` exp `]` `of` exp       (AST de type ArrayExp)
                 
~~~

Tiger autorise à définir des types structure (``recordType``). Par exemple,

~~~
    let
        type personne = {name:string, age:int}
        var a : personne := personne {name="Marvin", age=42}
    in
        a.age := a.age + 1 
    end
~~~

(@) Implémentez les types structure

~~~
    recordType::= `{` fields `}`  (AST de type RecordType)

    fields ::=                    
        [ id `:` id  {`,` id `:` id } ]
        (AST de type LinkedList<Field>, list vide ou pas de Field)

    Il faut également créer une expression pour initialiser les types structure.

    exp ::= recordExp

    recordExp ::= id `{` efields `}`  (AST de type RecordExp)

    efields ::= [ id `=` exp { `,` id `=` exp } ] 
                (AST de type LinkedList<ExpField>, liste d'affectations)
~~~

Arrivés là, vous reconnaissez tout Tiger ! Félicitations :)

<img src='/tigergrader/static/support/Tiger.jpg' height='100px'>



### Spécifications syntaxiques du langage Tiger

Ces spécifications sont reprises de
<http://www.lrde.epita.fr/~akim/ccmp/tiger.txt>.  Merci à Akim Demaille de nous
avoir autorisé à les réutiliser dans le cadre de notre cours.

We use Extended BNF, with ``[`` and ``]`` for zero or once, and ``{`` and ``}``
for any number of repetition including zero.

     program ::=
         exp

     exp ::=
       # Literals.
         `nil`
       | integer
       | string

       # Array and record creations.
       | id `[` exp `]` `of` exp
       | id `{`[ id `=` exp { `,` id `=` exp } ] `}`

       # Variables, field, elements of an array.
       | var

       # Function call.
       | id `(` [ exp { `,` exp }] `)`

       # Operations.
       | `-` exp
       | exp op exp
       | `(` exps `)`

       # Assignment.
       | var `:=` exp

       # Control structures.
       | `if` exp `then` exp [`else` exp]
       | `while` exp `do` exp
       | `for` id `:=` exp `to` exp `do` exp
       | `break`
       | `let` decs `in` exps `end`

     var ::= id
       | var `.` id
       | var `[` exp `]`
     exps ::= [ exp { `;` exp } ]

     decs ::= { dec }
     dec ::=
       # Type declaration.
         `type` id `=` type
       # Variable declaration.
       | vardec
       # Function declaration.
       | `function` id `(` params `)` [ `:` id ] `=` exp

     params ::= [ id `:` id { `,` id `:` id } ]

     vardec ::= `var` id [ `:` id ] `:=` exp

       # Types.
     type ::=
        # Type alias.
        id
        # Record type definition.
        | `{` fields  `}`
        # Array type definition.
        | `array` `of` id 
     fields ::= [ id `:` id { `,` id `:` id } ]

####Declarations
#####Type Declarations

**arrays**

The size of the array does not belong to the type.  Index of arrays
starts from 0 and ends at size - 1.

          let
            type int_array = array of int
            var  table := int_array[100] of 0
          in
            ...
          end

Arrays are initialized with the **same** instance of value.  This
leads to aliasing for entities with pointer semantics (strings,
arrays and records).

          let
            type rec = { val : int }
            type rec_arr = array of rec
            var  table := rec_arr[2] of rec { val = 42 }
          in
            table[0].val := 51
            /* Now table[1].val = 51. */
          end

Use a loop to instantiate several initialization values.

          let
            type rec = { val : int }
            type rec_arr = array of rec
            var  table := rec_arr[2] of nil
          in
            for i := 0 to 1 do
              table[i] := rec { val = 42 };
            table[0].val := 51
            /* table[1].val = 42. */
          end

**records**

Records are defined by a list of fields between braces.  Fields are
described as "fieldname : id" and are separated by a coma.
Field names are unique for a given record type.

          let
            type indexed_string = {index : int, value : string}
          in
            ...
          end

**recursive types**

Types can be recursive,

          let
            type stringlist = {head : string, tail : stringlist}
          in
            ...
          end

or mutually recursive (if they are declared in the same chunk) in
Tiger.

          let
            type indexed_string = {index : int, value : string}
            type indexed_string_list = {head : indexed_string, tail :
            indexed_string_list}
          in
            ...
          end

but there shall be no cycle.  This

          let
            type a = b
            type b = a
          in
            ...
          end

is invalid.

**type equivalence**

Two types are equivalent iff there are issued from the same type
construction (array or record construction, or primitive type).
As in C, unlike Pascal, structural equivalence is rejected.

Type aliases do not build new types, hence they are equivalent.

          let
            type a = int
            type b = int
            var a := 1
            var b := 2
          in
            a = b           /* OK */
          end

          let
            type a = {foo : int}
            type b = {foo : int}
            var va := a{foo = 1}
            var vb := b{foo = 2}
          in
            va = vb
          end

          is invalid, and must be rejected with exit status set to 5.

#####Variable Declarations

**variables**

There are two forms of variable declarations in Tiger: the short
one and the long one.

In the short form, only the name of the variable and the initial
value of the variable are specified, the variable type is
"inferred".

          let
            var foo := 1  /* foo is typed as an integer */
          in
            ...
          end

In the long form, the type of the variable is specified.  Since one
cannot infer a record type for ``nil``, the long form is mandated
when declaring a variable initialized to ``nil``.

          let
            type foo = {foo : int}
            var bar : foo := nil       /* Correct.   */
            var baz       := nil       /* Incorrect. */
          in
            ...
          end

#####Function Declarations

**functions**

To declare a function, provide its return value type:

          let
            function not (i : int) : int =
              if i = 0 then
                1
              else
                0
          in
            ...
          end

A procedure has no value return type.

          let
            function print_conditional (s : string, i : int) =
              if i then
                print (s)
              else
                print ("error")
          in
            print_string ("foo", 1)
          end

Functions can be recursive, but mutually recursive functions must
be in the same sequence of function declarations (no other
declaration should be placed between them).

####Expressions

**L-values**

The ``l-values`` (whose value can be read or changed) are: elements
of arrays, fields of records, arguments and variables.

**Valueless expressions**

Some expressions have no value: procedure calls, assignments, ``if``s
with no ``else`` clause, loops and ``break``.  Empty sequences (``()``)
and ``let``s with an empty body are also valueless.

**Nil**

The reserved word ``nil`` refers to a value from a ``record`` type.
Do not use ``nil`` where its type cannot be determined.

          let
            type any_record = {any : int}
            var nil_var : any_record := nil
            function nil_test(parameter : any_record) : int = ...
            var invalid := nil             /* no type, invalid */
          in
            if nil <> nil_var then
              ...
            if nil_test (nil_var) then
              ...
            if nil = nil then ...         /* no type, invalid */
          end

**Integers**

An integer literal is a series of decimal digits (therefore it is
non-negative).  Since the compiler aims at 32-bit architectures,
since it needs to handle signed integers, a literal integer value
must fit in a signed 32-bit integer.  Any other integer value is a
scanner error.

**Booleans**

There is no Boolean type in Tiger: they are encoded as integers,
with the same semantics as in C, i.e., 0 is the only value
standing for "false", anything else stands for "true".

**Strings**

A string constant is a possibly empty series of printable
characters, spaces or escapes sequences (see *note Lexical
Specifications::) enclosed between double quotes.

          let
            var s := "\t\124\111\107\105\122\n"
          in
            print(s)
          end

**Function call**

Function arguments are evaluated from the left to the right.
Arrays and records arguments are passed by reference, strings and
integer are passed by value.

The following example:

          let
            type my_record = {value : int}
            function reference(parameter : my_record) =
              parameter.value := 42
            function value(parameter : string) =
              parameter := "Tiger is the best language\n"
            var rec1 := my_record{value = 1}
            var str := "C++ rulez"
          in
            reference (rec1);
            print_int (rec1.value);
            print ("\n");
            value (str);
            print (str);
            print ("\n")
          end

results in:

          42

          C++ rulez

**Boolean operators**

Tiger Boolean operators normalize their result to 0/1.  For
instance, because ``&`` and ``|`` can be implemented as syntactic
sugar, one could easily make ``123 | 456`` return ``1`` or ``123``: make
them return ``1``.  Andrew Appel does not enforce this for ``&`` and
``|``; we do, so that the following program has a well defined
behavior:

          print_int ("0" < "9" | 42)

**Arithmetic**

Arithmetic expressions only apply on integers and return integers.
Available operators in Tiger are : +,-,* and /.

**Comparison**

Comparison operators (``=``, ``<>``, and ``<=``, ``<``, ``>=``, ``>``) return
a Boolean value.

* **Integer and string comparison**
   All the comparison operators apply to pairs of strings and
   pairs of integers, with obvious semantics.

* **String comparison**
   Comparison of strings is based on the lexicographic order.

* **Array and record comparison**
   Pairs of arrays and pairs of records **of the same type** can be
   compared for equality (``=``) and inequality (``<>``).  Identity
   equality applies, i.e., an array or a record is only equal to
   itself (shallow equality), regardless of the contents
   equality (deep equality). The value ``nil`` can be compared
   against a value which type is that of a record, e.g. `nil =
   nil' is invalid.
   Arrays and records cannot be ordered: ``<``, ``>``, ``<=``, ``>=``
   are valid only for pairs of strings or integers.

* **Void comparison**
   In conformance with A. Appel's specifications, any two void
   entities are equal.

**Assignment**

Assignments yield no value.  The following code is syntactically
correct, but type incorrect:

          let
            var foo := 1
            var bar := 1
          in
            foo := (bar := 2) + 1
          end

Note that the following code is valid:

          let
            var void1 := ()
            var void2 := ()
            var void3 := ()
          in
            void1 := void2 := void3 := ()
          end

**Array and record assignment**

Array and record assignments are shallow, not deep, copies.
Therefore aliasing effects arise: if an array or a record variable
A is assigned another variable B of the same type, then changes on
B will affect A and vice versa.

          let
            type bar = {foo : int}
            var rec1 := bar{foo = 1}
            var rec2 := bar{foo = 2}
          in
            print_int(rec1.foo);
            print(" is the value of rec1\n");
            print_int(rec2.foo);
            print(" is the value of rec2\n");
            rec1 := rec2;
            rec2.foo = 42;
            print_int(rec1.foo);
            print(" is the new value of rec1\n")
          end

**Sequences**

A sequence is a possibly empty series of expressions separated by
semicolons and enclosed by parenthesis. By convention, there are no
sequences of a single expression (see the following item).  The
sequence is evaluated from the left to the right.  The value of
the whole sequence is that of its last expression.

          let
            var a := 1
          in
            a := (
                   print ("first exp to display\n");
                   print ("second exp to display\n");
                   a := a + 1;
                   a
                 ) + 42;
            print ("the last value of a is : ");
            print_int (a);
            print ("\n")
          end

**Parentheses**

Parentheses enclosing a single expression enforce syntactic
grouping.

**Lifetime**

Records and arrays have infinite lifetime: their values lasts
forever even if the scope of their creation is left.

          let
            type bar = {foo : int}
            var rec1 := bar{foo = 1}
          in
            rec1 := let
                      var rec2 := bar{foo = 42}
                    in
                      rec2
                    end;
            print_int(rec1.foo);
            print("\n")
          end

**if-then-else**

In an if-expression:

          if EXP1 then
            EXP2
          else
            EXP3

EXP1 is typed as an integer, EXP2 and EXP3 must have the same type
which will be the type of the entire structure.  The resulting
type cannot be that of ``nil``.

**if-then**

In an if-expression:

          if EXP1 then
            EXP2

EXP1 is typed as an integer, and EXP2 must have no value.  The
whole expression has no value either.

**while**

In a while-expression:

          while EXP1 do
            EXP2

EXP1 is typed as an integer, EXP2 must have no value.  The whole
expression has no value either.

**for**

The following ``for`` loop

          for ID := EXP1 to EXP2 do
            EXP3

introduces a fresh variable, ID, which ranges from the value of
EXP1 to that of EXP2, inclusive, by steps of 1.  The scope of ID
is restricted to EXP3.  The variable ID cannot be assigned to.
The type of both EXP1 and EXP2 is integer, they can range from the
minimal to the maximal integer values.  The body EXP3 and the
whole loop have no value.

**break**

A break terminates the nearest enclosing loop (``while`` or ``for``).
A break must be enclosed by a loop.  A break cannot appear inside
a definition (e.g., between ``let`` and ``in``), except if it is
enclosed by a loop, of course.

**let**

In the let-expression:

          let
            DECS
          in
            EXPS
          end

DECS is a sequence of declaration and EXPS is a sequence of
expressions separated by a semi-colon.  The whole expression has
the value of EXPS.



